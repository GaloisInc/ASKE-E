[-- Description: A simple Susceptible/Infected/Removed model. --]
model SIR_MetaParam:
  [-- Description: The average number of contacts per person per time, multiplied by the probability of disease transmission in a contact between a susceptible and an infectious subject --]
  parameter beta = 0.4

  [-- Description: Rate of recovery from infection --]
  parameter gamma = 0.04

  [-- Description: Initial population of suceptible people. --]
  parameter s_initial = 997

  [-- Description: Initial population of infected people. --]
  parameter i_initial = 3

  [-- Description: Initial population of recovered people. --]
  parameter r_initial = 0


  [-- Description: Susceptible population --]
  state S = s_initial

  [-- Description: Infected population --]
  state I = i_initial

  [-- Description: Recovered population --]
  state R = r_initial

  let total_population = S + I + R

  event Infect:
    when:
      S > 0 and I > 0
    rate: 
      beta * S * I / total_population
    effect:
      S -= 1
      I += 1
      
  event Remove:
    when:
      I > 0
    rate: 
      gamma * I
    effect:
      I -= 1
      R += 1
      
      